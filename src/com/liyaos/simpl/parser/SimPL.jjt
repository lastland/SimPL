options {
    STATIC = false;
    VISITOR = true;
    MULTI = true;
    NODE_DEFAULT_VOID = true;
    NODE_CLASS = "SimPLNode";
    KEEP_LINE_COLUMN = true;
}

PARSER_BEGIN (SimPLParser)
    package com.liyaos.simpl.parser;
    import java.io.FileInputStream;
    import java.io.File;
    import java.io.FileNotFoundException;
    class SimPLParser {
	public static void main(String[] args)
	throws ParseException, TokenMgrError, FileNotFoundException {
	    SimPLParser parser = new SimPLParser(new FileInputStream(new File("test_input.txt")));
	    parser.Start();
	    System.out.println("Success!");
	}
    }
PARSER_END (SimPLParser)

SKIP: {" " | "\t"}
SKIP: {"\n" | "\r" | "\r\n"}
// key words
TOKEN: {
    <LET: "let">
    | <IN: "in">
    | <END: "end">
    | <IF: "if">
    | <THEN: "then">
    | <ELSE: "else">
    | <WHILE: "while">
    | <DO: "do">
    | <FST: "fst">
    | <SND: "snd">
    | <HEAD: "head">
    | <TAIL: "tail">
    | <FUN: "fun">
    | <NIL: "nil">
}
// program operators
TOKEN: {
    <CONS: "::">
    | <OPAR: "(">
    | <CPAR: ")">
    | <COMMA: "," >
    | <ASSIGN: ":=">
    | <SEMICOLON: ";">
    | <DEFUN: "->">
}
// data type
TOKEN: {
    <INT: (["0"-"9"])+>
    | <BOOL: "true" | "false">
}
// bop
TOKEN: {
    <PLUS: "+">
    | <MINUS: "-">
    | <TIMES: "*">
    | <DIVIDE: "/">
    | <EQ: "=">
    | <GREATERTHAN: ">">
    | <LESSTHAN: "<">
    | <AND: "and">
    | <OR: "or">
}
// uop
TOKEN: {
    <UMINUS: "~">
    | <NOT: "not">
}
// identifier
TOKEN: {
    <IDENTIFIER: ["a"-"z"](["a"-"z", "0"-"9"])*>
}

ASTProgram Start() #Program:
{}
{
    Expression()
    <EOF>
    {
	return jjtThis;
    }
}

void Expression():
{}
{
    Sequence()
}

void LetExpression() #Let :
{}
{
    <LET>
    Identifier()
    <EQ>
    Expression()
    <IN>
    Expression()
    <END> 
}

void IfExpression() #If :
{}
{
    <IF>
    Expression()
    <THEN>
    Expression()
    <ELSE>
    Expression()
    <END>
}

void WhileExpression() #While :
{}
{
    <WHILE>
    Expression()
    <DO>
    Expression()
    <END> 
}

void Sequence():
{}
{
    (OneExpression()
    (<SEMICOLON>OneExpression())*) #Sequence(>1)
}

void OneExpression() :
{}
{
    AnonymousFunction()
    | LetExpression()
    | IfExpression()
    | WhileExpression()
    | LOOKAHEAD(2) AssignmentExpression()
    | ConsExpression()
}

void AnonymousFunction() #AnonymousFunction:
{}
{
    <FUN> Identifier() <DEFUN>
    OneExpression()
}

void AssignmentExpression() #Assignment :
{}
{
    Identifier() <ASSIGN>OneExpression()
}

void ConsExpression():
{}
{
    AndOrExpression()
    (<CONS> AndOrExpression() #Cons(2))*
}

void AndOrExpression():
{}
{  
    CompareExpression()
    ((<AND> CompareExpression() #And(2))
    | (<OR> CompareExpression() #Or(2)))*
}

void CompareExpression():
{}
{
    AdditiveExpression()
    ((<EQ> AdditiveExpression() #Eq(2))
    | (<GREATERTHAN> AdditiveExpression() #GreaterThan(2))
    | (<LESSTHAN> AdditiveExpression() #LessThan(2)))*
}

void AdditiveExpression():
{}
{
    Term()
    ((<PLUS> Term() #Plus(2))
    | (<MINUS> Term() #Minus(2)))*
}

void Term():
{}
{
    Factor() 
    ((<TIMES> Factor() #Times(2))
    | (<DIVIDE> Factor() #Divide(2)))*
}

void Factor():
{}
{
    NegativeExpression()
    | NotExpression()
    | AtomExpression()
}

void NegativeExpression() #Negative :
{}
{
    <UMINUS> AtomExpression()
}

void NotExpression() #Not :
{}
{
    <NOT> AtomExpression()
}

void AtomExpression():
{}
{
    Integer()
    | Bool()
    | EmptyList()
    | Identifier()
    | FstExpression()
    | SndExpression()
    | HeadExpression()
    | TailExpression()
    // Can these `LOOKAHEAD` be eliminated?
    | LOOKAHEAD(BracketExpression()) BracketExpression()
    | LOOKAHEAD(PairExpression()) PairExpression()
    | AppExpression()
}

void Bool() #Bool :
{
    Token t;
}
{
    t = <BOOL>
    {
	jjtThis.setValue(t.image);
    }
}

void Integer() #Int :
{
    Token t;
}
{
    t = <INT>
    {
	jjtThis.setValue(t.image);
    }
}

void EmptyList() #Nil :
{}
{
    <NIL>
}

void Identifier() #Identifier :
{
    Token t;
}
{
    t = <IDENTIFIER>
    {
	jjtThis.setValue(t.image);
    }
}

void FstExpression() #Fst :
{}
{
    <FST> AtomExpression()
}

void SndExpression() #Snd:
{}
{
    <SND> AtomExpression()
}

void HeadExpression() #Head:
{}
{
    <HEAD> AtomExpression()
}

void TailExpression() #Tail:
{}
{
    <TAIL> AtomExpression()
}

void AppExpression() #Application :
{}
{
    <OPAR> Expression() Expression() <CPAR>
}

void PairExpression() #Pair :
{}
{
    <OPAR> Expression() <COMMA> Expression() <CPAR>
}

void BracketExpression() #Bracket :
{}
{
    <OPAR> Expression() <CPAR>
}
